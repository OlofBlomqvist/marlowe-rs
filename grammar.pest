WHITESPACE = _{ " " | "\t" | "\r" | "\n" | "\r\n" }
comma  = _{ "," }

lpar   = _{ "(" }
rpar   = _{ ")" }
lbra   = _{ "[" }
rbra   = _{ "]" }

string = ${ "\"" ~ str_inner ~ "\""  }
str_inner = @{ str_char* }
str_char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" )
}

Number = ${ "-"{0,1} ~ ASCII_DIGIT+ }

Bound = ${ "Bound" ~ WHITESPACE+ ~ Number ~ WHITESPACE+ ~ Number}
xBound = _{ 
    lpar ~ Bound ~ rpar | Bound
}

ChoiceId = ${ lpar ~ WHITESPACE* ~"ChoiceId" ~ WHITESPACE+ ~ string ~ WHITESPACE+ ~ Party ~ WHITESPACE* ~ rpar}


ValueId = @{ string }

TimeConstant = { ASCII_DIGIT+ }
TimeInterval = ${ lpar ~ WHITESPACE* ~ "TimeInterval" ~ WHITESPACE+ ~ Timeout ~ WHITESPACE+ ~ Timeout ~ WHITESPACE* ~ rpar }
TimeParam = ${ lpar ~ WHITESPACE* ~ "TimeParam" ~ WHITESPACE+ ~ string ~ WHITESPACE* ~ rpar }
Timeout = { TimeConstant | Number | TimeParam }

Party = _{ PkRoleOrAccount }

PkRoleOrAccount = _{ lpar ~ (Role | PK) ~ rpar }
Role = ${ "Role" ~ WHITESPACE+ ~ string}

PubKey = ${ "\"" ~ ("0"|"1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9"|"A"|"B"|"C"|"D"|"E"|"F"){64,64} ~ "\"" }

PK = ${ "PK" ~ WHITESPACE+ ~ PubKey }

Account = ${ "Account" ~ WHITESPACE+ ~ PkRoleOrAccount }

Token = ${ lpar ~ WHITESPACE* ~ "Token" ~ WHITESPACE+ ~ string ~ WHITESPACE+ ~ string ~ WHITESPACE* ~ rpar }

Value = _{ 
      ConstantParam
    | AvailableMoney
    | Cond
    | ChoiceValue
    | MulValue
    | DivValue
    | SubValue
    | AddValue
    | NegValue
    | UseValue
    | Constant
    | TimeIntervalStart
    | TimeIntervalEnd
 }
    TimeIntervalStart = { "TimeIntervalStart" }
    TimeIntervalEnd = { "TimeIntervalEnd" }
    Cond = ${ lpar ~ WHITESPACE* ~ "Cond" ~ WHITESPACE+ ~ Observation ~ WHITESPACE+ ~ Value ~ WHITESPACE+ ~ Value ~ WHITESPACE* ~ rpar}
    ChoiceValue = ${ lpar ~ WHITESPACE* ~ "ChoiceValue" ~ WHITESPACE+ ~ ChoiceId ~ WHITESPACE* ~ rpar}
    MulValue = ${ lpar ~ WHITESPACE* ~ "MulValue" ~ WHITESPACE+ ~ Value ~ WHITESPACE+ ~ Value ~ WHITESPACE* ~ rpar }
    DivValue = ${ lpar ~ WHITESPACE* ~ "DivValue" ~ WHITESPACE+ ~ Value ~ WHITESPACE+ ~ Value ~ WHITESPACE* ~ rpar}
    SubValue = ${ lpar ~ WHITESPACE* ~ "SubValue" ~ WHITESPACE+ ~ Value ~ WHITESPACE+ ~ Value ~ WHITESPACE* ~ rpar}
    AddValue = ${ lpar ~ WHITESPACE* ~ "AddValue" ~ WHITESPACE+ ~ Value ~ WHITESPACE+ ~ Value ~ WHITESPACE* ~ rpar}
    NegValue = ${ lpar ~ WHITESPACE* ~ "NegValue" ~ WHITESPACE+ ~ Value ~ WHITESPACE+ ~ Value ~ rpar}
    UseValue = ${ lpar ~ WHITESPACE* ~ "UseValue" ~ WHITESPACE+ ~ ValueId ~ WHITESPACE* ~ rpar}
    ConstantParam = ${ lpar ~ WHITESPACE* ~ "ConstantParam" ~ WHITESPACE+ ~ string ~ WHITESPACE* ~ rpar }
    Constant = ${ (lpar ~ WHITESPACE*){0,1} ~ "Constant" ~ WHITESPACE+ ~ Number ~ (WHITESPACE* ~ rpar){0,1} }
    AvailableMoney = ${ lpar ~ WHITESPACE* ~ "AvailableMoney" ~ WHITESPACE+ ~ Party ~ WHITESPACE+ ~ Token ~ WHITESPACE* ~ rpar}

Observation = _{ TrueObs | FalseObs | ValueEQ | ValueLE | 
                 ValueLT | ValueGT | ValueGE | OrObs | 
                 NotObs | AndObs | ChoseSomething }
    ValueEQ = ${ lpar ~ WHITESPACE* ~ "ValueE"  ~ WHITESPACE+  ~ Value  ~ WHITESPACE+ ~ Value ~WHITESPACE* ~ rpar }
    ValueLE = ${ lpar ~ WHITESPACE* ~ "ValueLE" ~ WHITESPACE+  ~ Value ~ WHITESPACE+  ~ Value ~WHITESPACE*  ~ rpar }
    ValueLT = ${ lpar ~ WHITESPACE* ~ "ValueLT" ~ WHITESPACE+  ~ Value ~ WHITESPACE+  ~ Value ~WHITESPACE*  ~ rpar }
    ValueGT = ${ lpar ~ WHITESPACE* ~ "ValueGT" ~ WHITESPACE+  ~ Value ~ WHITESPACE+  ~ Value ~WHITESPACE*  ~ rpar }
    ValueGE = ${ lpar ~ WHITESPACE* ~ "ValueGE" ~ WHITESPACE+  ~ Value ~ WHITESPACE+  ~ Value ~WHITESPACE*  ~ rpar }
    TrueObs = { "TrueObs" }
    FalseObs = { "FalseObs" }
    ChoseSomething = @{ lpar ~ WHITESPACE* ~ "ChoseSomething" ~ WHITESPACE+ ~ ChoiceId ~WHITESPACE*~ rpar }
    NotObs = ${ lpar ~ WHITESPACE* ~ "NotObs" ~ WHITESPACE+ ~ Observation ~ WHITESPACE* ~ rpar }
    OrObs =  ${ lpar ~ WHITESPACE* ~ "OrObs"  ~ WHITESPACE+ ~ Observation ~ WHITESPACE+ ~ Observation ~ WHITESPACE* ~ rpar }
    AndObs = ${ lpar ~ WHITESPACE* ~ "AndObs" ~ WHITESPACE+ ~ Observation ~ WHITESPACE+ ~ Observation ~ WHITESPACE* ~ rpar }
   
Action = @{ Case | Notify | Choice | Deposit }
    Deposit = ${ lpar ~ WHITESPACE* ~"Deposit" ~ WHITESPACE+ ~ Party ~ WHITESPACE+ ~ Party ~ WHITESPACE+ ~ Token ~ WHITESPACE+ ~ Value ~ WHITESPACE* ~ rpar }
    Choice =  ${ lpar ~ WHITESPACE* ~"Choice"  ~ WHITESPACE+ ~ ChoiceId ~ WHITESPACE+ ~ ArrayOfBounds ~ WHITESPACE* ~ rpar }
    Notify =  ${ lpar ~ WHITESPACE* ~"Notify"  ~ WHITESPACE+ ~ Observation ~ WHITESPACE* ~ rpar }

Case = ${ 
    lpar ~ WHITESPACE* ~ "Case" ~ WHITESPACE+ ~ Action ~ WHITESPACE+ ~ WrappedContract ~ WHITESPACE* ~ rpar
    | "Case" ~ WHITESPACE+ ~ Action ~ WHITESPACE+ ~ WrappedContract
}

PayeeAccount = ${ "Account" ~ WHITESPACE+ ~ Party }
PayeeParty = ${ "Party" ~ WHITESPACE+ ~ Party}

Payee = _{ 
    lpar ~ (PayeeAccount|PayeeParty) ~ rpar 
}

MainContract = { Contract ~ EOI }

Contract = _{ Close | When | If | Let | Assert | Pay }
    When   = ${ "When" ~ WHITESPACE+ ~ ArrayOfCases ~ WHITESPACE+ ~ Timeout ~ WHITESPACE+ ~ WrappedContract }
    Pay    = ${ "Pay"  ~ WHITESPACE+ ~ Party ~ WHITESPACE+ ~ Payee ~ WHITESPACE+ ~ Token ~ WHITESPACE+ ~ Value  ~ WHITESPACE+ ~ WrappedContract }
    If     = ${ "If"   ~ WHITESPACE+ ~ Observation ~ WHITESPACE+ ~ WrappedContract ~ WHITESPACE+ ~ WrappedContract }
    Let    = ${ "Let"  ~ WHITESPACE+ ~ string ~ WHITESPACE+ ~ Value ~ WHITESPACE+ ~ WrappedContract }
    Assert = ${ "Assert" ~ WHITESPACE+ ~ Observation ~ WHITESPACE+ ~ WrappedContract }
    Close  = { "Close" }

WrappedContract = ${
    Close | "(" ~ WHITESPACE* ~ (Assert|Let|If|Pay|When) ~ WHITESPACE* ~  ")"
}

ArrayOfCases = ${ 
     lbra ~ WHITESPACE* ~ Case ~ (WHITESPACE* ~ "," ~ WHITESPACE* ~ Case)* ~ WHITESPACE* ~ rbra 
     | lbra ~ WHITESPACE* ~ rbra
}

ArrayOfBounds = ${ 
     lbra ~ WHITESPACE* ~ xBound ~ (WHITESPACE* ~ "," ~ WHITESPACE* ~ xBound)* ~ WHITESPACE* ~ rbra 
     | lbra ~ WHITESPACE* ~ rbra
}
